Shallow Wintergreen Falcon

high

# Malicious users can arbitrarily change the balance of any account.

## Summary
The [`batchUpdateAccountToken`](https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/facets/AccountFacet.sol#L68) function is not protected. Malicious users can arbitrarily call `addToken` or `subToken`  for any account without making a deposit or withdrawal. 

## Vulnerability Detail
The `batchUpdateAccountToken` function in the `AccountFacet` contract is external and not protected by any access control. It calls `AssetsProcess.updateAccountToken` directly. 
```solidity
contract AccountFacet is IAccount {
    // ...
    function batchUpdateAccountToken(AssetsProcess.UpdateAccountTokenParams calldata params) external override {
        AddressUtils.validEmpty(params.account);
        AssetsProcess.updateAccountToken(params);
    }
    // ...
}
```
The `AssetsProcess.updateAccountToken` allows to add or sub tokens for arbitrary account. 
```solidity
    function updateAccountToken(UpdateAccountTokenParams calldata params) external {
        Account.Props storage accountProps = Account.load(params.account);
        accountProps.checkExists();
        for (uint256 i; i < params.tokens.length; i++) {
            if (params.changedTokenAmounts[i] == 0) {
                continue;
            }
            if (params.changedTokenAmounts[i] > 0) {
                accountProps.addToken(params.tokens[i], params.changedTokenAmounts[i].toUint256());
                accountProps.repayLiability(params.tokens[i]);
            } else {
                accountProps.subToken(params.tokens[i], (-params.changedTokenAmounts[i]).toUint256());
            }
        }
    }
```
Meanwhile, a normal user needs to `depositToVault` before its token can be added. 
```solidity
    function deposit(DepositParams calldata params) external {
        address token = depositToVault(params);
        // ... 
        accountProps.addToken(token, params.amount, Account.UpdateSource.DEPOSIT);
        // ... 
    }
```

## Impact
Malicious users can arbitrarily change the balance of any account.

## Code Snippet
- https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/facets/AccountFacet.sol#L68
- https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/process/AssetsProcess.sol#L179

## Tool used

Manual Review

## Recommendation

Delete the `batchUpdateAccountToken` function or add proper access control (e.g., an `OnlyOwner` modifier). 
