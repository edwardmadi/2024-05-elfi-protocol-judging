Great Maroon Wasp

high

# The `executeUpdatePositionMarginRequest` function is missing the operation to update the borrowing fee


## Summary
The `executeUpdatePositionMarginRequest` function is missing the operation to update the borrowing fee

## Vulnerability Detail
```javascript
    function executeUpdatePositionMarginRequest(
        uint256 requestId,
        OracleProcess.OracleParam[] calldata oracles
    ) external override {
        uint256 startGas = gasleft();
        RoleAccessControl.checkRole(RoleAccessControl.ROLE_KEEPER);
        UpdatePositionMargin.Request memory request = UpdatePositionMargin.get(
            requestId
        );
        if (request.account == address(0)) {
            revert Errors.UpdatePositionMarginRequestNotExists();
        }
        OracleProcess.setOraclePrice(oracles);
@>>        PositionMarginProcess.updatePositionMargin(requestId, request);
        OracleProcess.clearOraclePrice();
        GasProcess.processExecutionFee(
            GasProcess.PayExecutionFeeParams(
                request.isExecutionFeeFromTradeVault
                    ? IVault(address(this)).getTradeVaultAddress()
                    : IVault(address(this)).getPortfolioVaultAddress(),
                request.executionFee,
                startGas,
                msg.sender,
                request.account
            )
        );
    }
```
The executeUpdatePositionMarginRequest calls PositionMarginProcess.updatePositionMargin() function.
```javascript
function updatePositionMargin(uint256 requestId, UpdatePositionMargin.Request memory request) external {
       //skip ...
        if (request.isAdd) {
            //skip ....
            _executeAddMargin(position, cache);
            
        } else {
            uint256 reduceMarginAmount = _executeReduceMargin(position, symbolProps, request.updateMarginAmount, true);
            //skip ....
        }
    //skip ...    
    }


```
But both in `_executeAddMargin` and `_executeReduceMargin` , here is only a change in the holdPoolAmount of the position, but the corresponding borrowing fee is not updated.
```javascript
function _executeAddMargin(Position.Props storage position, AddPositionMarginCache memory cache) internal {
        if (
            cache.addMarginAmount >
            CalUtils.usdToToken(
                position.qty - position.initialMarginInUsd,
                cache.marginTokenDecimals,
                cache.marginTokenPrice
            )
        ) {
            revert Errors.AddMarginTooBig();
        }
        position.initialMargin += cache.addMarginAmount;
        if (cache.isCrossMargin) {
            position.initialMarginInUsd = CalUtils.divRate(position.qty, position.leverage);
            position.initialMarginInUsdFromBalance += cache.addInitialMarginFromBalance;
        } else {
            position.initialMarginInUsd += CalUtils.tokenToUsd(
                cache.addMarginAmount,
                cache.marginTokenDecimals,
                cache.marginTokenPrice
            );
            position.leverage = CalUtils.divRate(position.qty, position.initialMarginInUsd);
            position.initialMarginInUsdFromBalance = position.initialMarginInUsd;
        }

        uint256 subHoldAmount = cache.addMarginAmount.min(position.holdPoolAmount);
@>>        position.holdPoolAmount -= subHoldAmount;
        LpPoolProcess.updatePnlAndUnHoldPoolAmount(cache.stakeToken, position.marginToken, subHoldAmount, 0, 0);
    }
```

```javascript
function _executeReduceMargin(
        Position.Props storage position,
        Symbol.Props memory symbolProps,
        uint256 reduceMargin,
        bool needUpdateLeverage
    ) internal returns (uint256) {
        AppConfig.SymbolConfig memory symbolConfig = AppConfig.getSymbolConfig(symbolProps.code);
        uint256 maxReduceMarginInUsd = position.initialMarginInUsd -
            CalUtils.divRate(position.qty, symbolConfig.maxLeverage).max(
                AppTradeConfig.getTradeConfig().minOrderMarginUSD
            );
        if (reduceMargin > maxReduceMarginInUsd) {
            revert Errors.ReduceMarginTooBig();
        }
        uint8 decimals = TokenUtils.decimals(position.marginToken);
        uint256 marginTokenPrice = OracleProcess.getLatestUsdUintPrice(position.marginToken, !position.isLong);
        uint256 reduceMarginAmount = CalUtils.usdToToken(reduceMargin, decimals, marginTokenPrice);
        if (
            position.isCrossMargin &&
            position.initialMarginInUsd - position.initialMarginInUsdFromBalance < reduceMargin
        ) {
            position.initialMarginInUsdFromBalance -= (reduceMargin -
                (position.initialMarginInUsd - position.initialMarginInUsdFromBalance)).max(0);
        }
        position.initialMargin -= reduceMarginAmount;
        position.initialMarginInUsd -= reduceMargin;
        if (needUpdateLeverage) {
            position.leverage = CalUtils.divRate(position.qty, position.initialMarginInUsd);
        }
        if (!position.isCrossMargin) {
            position.initialMarginInUsdFromBalance = position.initialMarginInUsd;
        }

@>>        position.holdPoolAmount += reduceMarginAmount;
        LpPoolProcess.holdPoolAmount(symbolProps.stakeToken, position.marginToken, reduceMarginAmount, position.isLong);
        return reduceMarginAmount;
    }
```

## Impact
This results in incorrect calculation of the borrowing fee, potentially causing financial loss for the user or the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/facets/PositionFacet.sol#L61

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L91

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L340

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L370
## Tool used

Manual Review

## Recommendation
add FeeProcess.updateBorrowingFee(position, symbolProps.stakeToken) in the `updatePositionMargin` function
