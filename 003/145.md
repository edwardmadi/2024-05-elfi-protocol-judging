Proper Tartan Llama

medium

# Keeper couldn't recognize the loss of order execution fee.

## Summary
`GasProcess.sol#processExecutionFee` function never call `CommonData.sol#addLossExecutionFee` function.
So the keeper couldn't recognize the loss of order execution fee.

## Vulnerability Detail
`GasProcess.sol#processExecutionFee` function is the following.
```solidity
    function processExecutionFee(PayExecutionFeeParams memory cache) external {
        uint256 usedGas = cache.startGas - gasleft();
        uint256 executionFee = usedGas * tx.gasprice;
        uint256 refundFee;
        uint256 lossFee;
22:     if (executionFee > cache.userExecutionFee) {
23:         executionFee = cache.userExecutionFee;
24:         lossFee = executionFee - cache.userExecutionFee;
        } else {
            refundFee = cache.userExecutionFee - executionFee;
        }
        VaultProcess.transferOut(
            cache.from,
            AppConfig.getChainConfig().wrapperToken,
            address(this),
            cache.userExecutionFee
        );
        VaultProcess.withdrawEther(cache.keeper, executionFee);
        if (refundFee > 0) {
            VaultProcess.withdrawEther(cache.account, refundFee);
        }
        if (lossFee > 0) {
39:         CommonData.addLossExecutionFee(lossFee);
        }
    }
```
When `cache.userExecutionFee` is bigger than `executionFee` in `L22`, by `L23`, `lossFee` will always be zero in `L24`. 
So the `CommonData.sol#addLossExecutionFee` in `L39` is never called.

## Impact
If keeper recognize the lack of `userExecutionFee`, he should increase the `placeIncreaseOrderGasFeeLimit`, `mintGasFeeLimit` etc of `chainConfig` to prevent his own loss, but now, it's impossible.
This issue causes damage to the protocol keeper.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/GasProcess.sol#L23
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/GasProcess.sol#L24

## Tool used
Manual Review

## Recommendation
Swap `L23` with `L24` as follows.
```solidity
    function processExecutionFee(PayExecutionFeeParams memory cache) external {
        ...
        if (executionFee > cache.userExecutionFee) {
--          executionFee = cache.userExecutionFee;
--          lossFee = executionFee - cache.userExecutionFee;
++          lossFee = executionFee - cache.userExecutionFee;
++          executionFee = cache.userExecutionFee;
        ...
    }
```