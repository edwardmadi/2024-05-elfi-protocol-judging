Custom Cider Guppy

high

# [H-1] Incorrect `lossFee` Calculation in `GasProcess::processExecutionFee` to Resulting in Keeper's Financial Loss


## Summary
The `processExecutionFee` function is utilized whenever a `keeper` processes a request paid for by a user to compensate the keeper for gas expenses. If the actual fee spent, `executionFee`, exceeds the fee provided by the user, `cache.userExecutionFee`, the difference is intended to be recorded as a loss in the `CommonData` storage via the `CommonData.addLossExecutionFee()` method. However, the calculation for `lossFee` is flawed, resulting in `lossFee` always being set to zero.


## Vulnerability Detail
The following code snippet from `processExecutionFee` illustrates the issue. Lines 23 and 24 adjust `executionFee` to match `cache.userExecutionFee` if the former is greater, to prevent transaction reversion due to insufficient contract balance. Consequently, when calculating `lossFee`, subtracting identical values results in a `lossFee` of zero.

```javascript
    function processExecutionFee(PayExecutionFeeParams memory cache) external {
        uint256 usedGas = cache.startGas - gasleft();
        uint256 executionFee = usedGas * tx.gasprice;
        uint256 refundFee;
        uint256 lossFee;
        if (executionFee > cache.userExecutionFee) {
@>          executionFee = cache.userExecutionFee;
@>          lossFee = executionFee - cache.userExecutionFee;
        } else {
            refundFee = cache.userExecutionFee - executionFee;
        }
        VaultProcess.transferOut(
            cache.from,
            AppConfig.getChainConfig().wrapperToken,
            address(this),
            cache.userExecutionFee
        );
        VaultProcess.withdrawEther(cache.keeper, executionFee);
        if (refundFee > 0) {
            VaultProcess.withdrawEther(cache.account, refundFee);
        }
        if (lossFee > 0) {
            CommonData.addLossExecutionFee(lossFee);
        }
    }
```

## Impact
While the exact impact is unclear due to lack of visibility into how keepers manage `lossExecutionFees`, this issue likely results in keepers losing fees without understanding why. and since there is no check in functions such as `StakeFacet::createMintStakeTokenRequest` and `StakeFacet::executeMintStakeToken` to check wether enough Execution Fee is sent by user or not, its safe to assume that its keepers job to stop executing requests when the `totalLossExecutionFee` reaches a certain treshhold. as a result users can just ignore the executionFee parameter and set it to zero.

## Code Snippet
[Link to Code](https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/GasProcess.sol#L24)
## Tool used
Manual Review

## Recommendation
To resolve this issue, calculate `lossFee` before adjusting `executionFee` to the user-specified value:

```diff
    function processExecutionFee(PayExecutionFeeParams memory cache) external {
        uint256 usedGas = cache.startGas - gasleft();
        uint256 executionFee = usedGas * tx.gasprice;
        uint256 refundFee;
        uint256 lossFee;
        if (executionFee > cache.userExecutionFee) {
-           executionFee = cache.userExecutionFee;
-           lossFee = executionFee - cache.userExecutionFee;
+           lossFee = executionFee - cache.userExecutionFee;
+           executionFee = cache.userExecutionFee;
        } else {
            refundFee = cache.userExecutionFee - executionFee;
        }
        VaultProcess.transferOut(
            cache.from,
            AppConfig.getChainConfig().wrapperToken,
            address(this),
            cache.userExecutionFee
        );
        VaultProcess.withdrawEther(cache.keeper, executionFee);
        if (refundFee > 0) {
            VaultProcess.withdrawEther(cache.account, refundFee);
        }
        if (lossFee > 0) {
            CommonData.addLossExecutionFee(lossFee);
        }
    }
```