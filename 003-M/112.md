Ripe Hemp Perch

Medium

# User cap amounts can be breached to DoS other users depositing

## Summary
Elfi's user cap amounts for the amount users can deposit for any token can be breached when users make deposits in intervals or all at once.

## Vulnerability Detail
```solidity
FILE: AccountFacet.sol

function deposit(address token, uint256 amount) external payable override { // @note entry point for deposits
        if (amount == 0) {
            revert Errors.AmountZeroNotAllowed();
        }
        bool isNativeToken = token == address(0);
        if (isNativeToken && msg.value != amount) {
            revert Errors.AmountNotMatch(msg.value, amount);
        }
        if (!isNativeToken && !AppTradeTokenConfig.getTradeTokenConfig(token).isSupportCollateral) {
            revert Errors.OnlyCollateralSupported();
        }
        address account = msg.sender;

        AssetsProcess.deposit(
            AssetsProcess.DepositParams(
                account,
                isNativeToken ? AppConfig.getChainConfig().wrapperToken : token,
                amount,
                AssetsProcess.DepositFrom.MANUAL,
                isNativeToken
            )
        );
    }

FILE: AssetsProcess.sol
function deposit(DepositParams calldata params) external {
        ...

            if (collateralAmount + params.amount > tradeTokenConfig.collateralTotalCap) {
                revert Errors.CollateralTotalCapOverflow(token, tradeTokenConfig.collateralTotalCap);
            }
            // @audit can breach because the amount being deposited is not taken into account + amount being deposited before just like it is being done for overall collateral amount cap
 @>           if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) { // @audit wrong logic
                revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
            }
            ...

         // @note user's internal accounting is incremented
         accountProps.addToken(token, params.amount, Account.UpdateSource.DEPOSIT);

        ...

        emit Deposit(params);
    }

FILE: Accounts.sol

function getTokenAmount(Props storage self, address token) public view returns (uint256) { // @note checking and returning user' internal balances for a supposed token
        return self.tokenBalances[token].amount;
    }
```

## Impact
Users can use this loophole to block deposits for other users which is essentially a DoS for the protocol's deposit functionality.
Assume the scenario below:

- Maximum collateral that can be posted for the WETH token overall is 1000 WETH
- Maximum collateral that any user can post in WETH is 100 WETH
- User A hasn't made a deposit before and noticed his cap is 100 WETH, a total overall cap is 1000 WETH but the logic for not breaching his 100 WETH cap is flawed when he has no prior deposit that has reached that cap
- User A deposits 1000 WETH, his cap of 100 WETH is breached here: `if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) {` because he has no prior deposits in his account so that returns 0 and 0 is < 100 WETH. So, he deposits 1000 WETH
- User B cannot deposit WETH because the 1k WETH overall cap has been reached as a result of User A breaching their 100 WETH cap

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/facets/AccountFacet.sol#L29
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/AssetsProcess.sol#L96

## Tool used

Manual Review

## Recommendation

```diff
- if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) {
-    revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
- }

+ if (accountProps.getTokenAmount(token) + params.amount > tradeTokenConfig.collateralUserCap) {
+    revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
+ }
```