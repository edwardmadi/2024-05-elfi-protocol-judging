Proper Tartan Llama

High

# Depositor may deposit in excess of `collateralUserCap`.

## Summary
Depositor may deposit in excess of `collateralUserCap`.
It will cause excessively large amount of bad debt to the protocol.

## Vulnerability Detail
`AssetsProcess.sol#deposit` function is the following.
```solidity
    function deposit(DepositParams calldata params) external {
        address token = depositToVault(params);
        Account.Props storage accountProps = Account.loadOrCreate(params.account);
        if (DepositFrom.MANUAL == params.from) {
            AppTradeTokenConfig.TradeTokenConfig memory tradeTokenConfig = AppTradeTokenConfig.getTradeTokenConfig(
                token
            );
            if (!tradeTokenConfig.isSupportCollateral) {
                revert Errors.TokenIsNotSupportCollateral();
            }
            CommonData.Props storage commonData = CommonData.load();
            uint256 collateralAmount = commonData.getTradeTokenCollateral(token);
            if (collateralAmount + params.amount > tradeTokenConfig.collateralTotalCap) {
                revert Errors.CollateralTotalCapOverflow(token, tradeTokenConfig.collateralTotalCap);
            }
96:         if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) {
                revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
            }
            commonData.addTradeTokenCollateral(token, params.amount);
        }
        if (accountProps.owner == address(0)) {
            accountProps.owner = params.account;
        }
        accountProps.addToken(token, params.amount, Account.UpdateSource.DEPOSIT);
        if (DepositFrom.MANUAL == params.from) {
            uint256 repayAmount = accountProps.repayLiability(token);
            if (params.amount > repayAmount) {
                uint256 requestId = UuidCreator.nextId(DEPOSIT_ID_KEY);
                PositionMarginProcess.updateAllPositionFromBalanceMargin(
                    requestId,
                    params.account,
                    token,
                    (params.amount - repayAmount).toInt256(),
                    ""
                );
            }
        }

        emit Deposit(params);
    }
```
As can be seen, `L96` doesn't take into account the `params.amount` in the condition inequality.
Thus a depositor can deposit large amount of collateral in excess of `collateralUserCap`.

`common.ts` shows that the `collateralUserCap` should be `1/1000` of `collateralTotalCap`.
If a depositor who has deposited huge amount of collateral causes bad debt, it can cause serious damage to the protocol.

## Impact
This issue may cause excessively large amount of bad debt to the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/AssetsProcess.sol#L96

## Tool used
Manual Review

## Recommendation
Modify `AssetsProcess.sol#deposit` function as follows.
```solidity
    function deposit(DepositParams calldata params) external {
        address token = depositToVault(params);
        Account.Props storage accountProps = Account.loadOrCreate(params.account);
        if (DepositFrom.MANUAL == params.from) {
            AppTradeTokenConfig.TradeTokenConfig memory tradeTokenConfig = AppTradeTokenConfig.getTradeTokenConfig(
                token
            );
            if (!tradeTokenConfig.isSupportCollateral) {
                revert Errors.TokenIsNotSupportCollateral();
            }
            CommonData.Props storage commonData = CommonData.load();
            uint256 collateralAmount = commonData.getTradeTokenCollateral(token);
            if (collateralAmount + params.amount > tradeTokenConfig.collateralTotalCap) {
                revert Errors.CollateralTotalCapOverflow(token, tradeTokenConfig.collateralTotalCap);
            }
--          if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) {
++          if (accountProps.getTokenAmount(token) + params.amount > tradeTokenConfig.collateralUserCap) {
                revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
            }
            commonData.addTradeTokenCollateral(token, params.amount);
        }
        if (accountProps.owner == address(0)) {
            accountProps.owner = params.account;
        }
        accountProps.addToken(token, params.amount, Account.UpdateSource.DEPOSIT);
        if (DepositFrom.MANUAL == params.from) {
            uint256 repayAmount = accountProps.repayLiability(token);
            if (params.amount > repayAmount) {
                uint256 requestId = UuidCreator.nextId(DEPOSIT_ID_KEY);
                PositionMarginProcess.updateAllPositionFromBalanceMargin(
                    requestId,
                    params.account,
                    token,
                    (params.amount - repayAmount).toInt256(),
                    ""
                );
            }
        }

        emit Deposit(params);
    }
```