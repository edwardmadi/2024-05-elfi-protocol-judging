Custom Pear Goldfish

High

# Attacker can decrease other user token balance.

## Summary
The function `AccountFacet::batchUpdateAccountToken` is used to update the tokens for a user. This function calls `AssetsProcess.updateAccountToken`, which then either increases token amount and repays the liabilities or decreases token amount. However that function can be called by anyone and pass other user's data.

## Vulnerability Detail
If we take a look at `batchUpdateAccountToken` function:

```solidity
    function batchUpdateAccountToken(AssetsProcess.UpdateAccountTokenParams calldata params) external override {
        AddressUtils.validEmpty(params.account);
        AssetsProcess.updateAccountToken(params);
    }
```

We can see that there is no validation for the `params` parameter. A malicious user can pass other user's address as `params.account`. Then `updateAccountToken` function:

```solidity
    function updateAccountToken(UpdateAccountTokenParams calldata params) external {
        Account.Props storage accountProps = Account.load(params.account);
        accountProps.checkExists();
        for (uint256 i; i < params.tokens.length; i++) {
            if (params.changedTokenAmounts[i] == 0) {
                continue;
            }
            if (params.changedTokenAmounts[i] > 0) {
                accountProps.addToken(params.tokens[i], params.changedTokenAmounts[i].toUint256());
                accountProps.repayLiability(params.tokens[i]);
            } else {
                accountProps.subToken(params.tokens[i], (-params.changedTokenAmounts[i]).toUint256());
            }
        }
    }
```

Here there is no validation either. The attacker will make sure to go into the `else` condition and then:

```solidity
    function subToken(Props storage self, address token, uint256 amount, UpdateSource source) public {
        require(self.tokens.contains(token), "token not exists!");
        require(self.tokenBalances[token].amount >= amount, "token amount not enough!");
        require(
            self.tokenBalances[token].amount >= self.tokenBalances[token].usedAmount + amount,
            "token amount exclude used amount not enough!"
        );
        TokenBalance memory preBalance = self.tokenBalances[token];
        self.tokenBalances[token].amount -= amount;
        emit AccountTokenUpdateEvent(self.owner, token, preBalance, self.tokenBalances[token], source);
    }
```

Here the balance of the token(the user must have this token) will be decreased.

## Impact
Attacker can decrease user's token balance.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/facets/AccountFacet.sol#L68C1-L71C6

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/process/AssetsProcess.sol#L179C1-L193C6

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/storage/Account.sol#L107C1-L117C6

## Tool used
Manual Review

## Recommendation
Add a validation in `batchUpdateAccountToken` function that will ensure that `msg.sender == params.account`.