Stale Bubblegum Osprey

high

# The `PositionMarginProcess.updateAllPositionFromBalanceMargin()` function is passing an incorrect parameter to the `updatePositionFromBalanceMargin()` function call

## Summary

In the `updateAllPositionFromBalanceMargin()` function, the `updatePositionFromBalanceMargin()` function call is using incorrect amount parameter.

## Vulnerability Detail

In the `updateAllPositionFromBalanceMargin()` function, the call to `updatePositionFromBalanceMargin()` consistently uses the variable `amount` as the 4th argument, as seen on `L293`. However, within each loop iteration, the `marginToken` amount is decremented to `reduceAmount` on `L295`. Therefore, the `updatePositionFromBalanceMargin()` function should be invoked with the `reduceAmount` parameter instead of `amount`. As a result, the total added or reduced margin amounts will be much greater than expected.

```solidity
274: function updateAllPositionFromBalanceMargin(
275:         uint256 requestId,
276:         address account,
277:         address token,
278:         int256 amount,
279:         bytes32 originPositionKey
280:     ) external {
281:         if (amount == 0) {
282:             return;
283:         }
284:         bytes32[] memory positionKeys = Account.load(account).getAllPosition();
285:         int256 reduceAmount = amount;
286:         for (uint256 i; i < positionKeys.length; i++) {
287:             Position.Props storage position = Position.load(positionKeys[i]);
288:             if (token == position.marginToken && position.isCrossMargin) {
289:                 int256 changeAmount = updatePositionFromBalanceMargin(
290:                     position,
291:                     originPositionKey.length > 0 && originPositionKey == position.key,
292:                     requestId,
293:                     amount
294:                 ).toInt256();
295:                 reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
296:                 if (reduceAmount == 0) {
297:                     break;
298:                 }
299:             }
300:         }
301:     }
```

## Impact

This will result in a discrepancy between the user's total reported margin amounts and the actual assets held in the vaults.

## Code Snippet

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L274-L301

## Tool used

Manual Review

## Recommendation

The parameter passed to the `updatePositionFromBalanceMargin()` function call should be `reduceAmount` instead of `amount`.

```diff
    function updateAllPositionFromBalanceMargin(
        uint256 requestId,
        address account,
        address token,
        int256 amount,
        bytes32 originPositionKey
    ) external {
        if (amount == 0) {
            return;
        }
        bytes32[] memory positionKeys = Account.load(account).getAllPosition();
        int256 reduceAmount = amount;
        for (uint256 i; i < positionKeys.length; i++) {
            Position.Props storage position = Position.load(positionKeys[i]);
            if (token == position.marginToken && position.isCrossMargin) {
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
-                   amount
+                   reduceAmount
                ).toInt256();
                reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
                if (reduceAmount == 0) {
                    break;
                }
            }
        }
    }
```