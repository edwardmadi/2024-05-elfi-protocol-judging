Dazzling Leather Sidewinder

high

# Incorrect implementation of the `PositionMarginProcess.updateAllPositionFromBalanceMargin()` function.

## Summary

In the `updateAllPositionFromBalanceMargin()` function, the value of the 4th parameter used when calling the `updatePositionFromBalanceMargin()` function is incorrect.

## Vulnerability Detail

As shown at `L293` of the `updateAllPositionFromBalanceMargin()` function, the 4th parameter used to call the `updatePositionFromBalanceMargin()` function is always the static `amount`, even though the amount of `marginToken` is reduced to `reduceAmount` in each loop. Therefore, the 4th parameter should be updated to use the `reduceAmount` instead. Consequently, the total updated margin amounts will be significantly greater than expected.

```solidity
    function updateAllPositionFromBalanceMargin(
        uint256 requestId,
        address account,
        address token,
        int256 amount,
        bytes32 originPositionKey
    ) external {
        if (amount == 0) {
            return;
        }
        bytes32[] memory positionKeys = Account.load(account).getAllPosition();
        int256 reduceAmount = amount;
        for (uint256 i; i < positionKeys.length; i++) {
            Position.Props storage position = Position.load(positionKeys[i]);
            if (token == position.marginToken && position.isCrossMargin) {
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
293                 amount
                ).toInt256();
                reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
                if (reduceAmount == 0) {
                    break;
                }
            }
        }
    }
```

## Impact

The user's total margin amounts will be much greater than the actual assets held in the vaults.

## Code Snippet

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L274-L301

## Tool used

Manual Review

## Recommendation

The `PositionMarginProcess.updateAllPositionFromBalanceMargin()` function should be fixed as follows.

```diff
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
-                   amount
+                   reduceAmount
                ).toInt256();
                reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
```