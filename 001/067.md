Overt Fiery Starfish

medium

# Incorrect changeAmount calculation in updatePositionFromBalanceMargin

## Summary
The calculation of `changeAmount` is incorrect, and traders may take some loss because of this incorrect calculation.

## Vulnerability Detail
When traders withdraw some collateral from AccountFacet::withdraw(), the system will update `initialMarginInUsdFromBalance` for all existing cross margin positions via `updateAllPositionFromBalanceMargin`.
In function `updateAllPositionFromBalanceMargin`, the function will loop all cross margin position to increase or decrease these positions' `initialMarginInUsdFromBalance`.

The vulnerability exists in function `updatePositionFromBalanceMargin()`. When `position.initialMarginInUsdFromBalance <= addBorrowMarginInUsd`, the system reset `position.initialMarginInUsdFromBalance` to 0 and then calculate the `changeAmount` using 0 vauled `position.initialMarginInUsdFromBalance`. So the return `changeAmount` will keep 0 in this case.

When we return back to function `updateAllPositionFromBalanceMargin`, function thinks that this position changes 0 amount of `position.initialMarginInUsdFromBalance` and will go on looping other positions. The result is that traders' decreased `position.initialMarginInUsdFromBalance` from all positions may be larger than `reduceAmount`. 

```javascript
    function updateAllPositionFromBalanceMargin(
        uint256 requestId,
        address account,
        address token,
        int256 amount,
        bytes32 originPositionKey
    ) external {
        if (amount == 0) {
            return;
        }
        bytes32[] memory positionKeys = Account.load(account).getAllPosition();
        int256 reduceAmount = amount;
        for (uint256 i; i < positionKeys.length; i++) {
            Position.Props storage position = Position.load(positionKeys[i]);
            if (token == position.marginToken && position.isCrossMargin) {
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
                    amount
                ).toInt256();
                reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
                if (reduceAmount == 0) {
                    break;
                }
            }
        }
    }
    function updatePositionFromBalanceMargin(
        Position.Props storage position,
        bool needSendEvent,
        uint256 requestId,
        int256 amount
    ) public returns (uint256 changeAmount) {
        // if all initial margin of this position comes fromt the traders' account,
        if (position.initialMarginInUsd == position.initialMarginInUsdFromBalance || amount == 0) {
            changeAmount = 0;
            return 0;
        }
        // add collateral
        if (amount > 0) {
            // calculate borrow margin account from initialMargin
            uint256 borrowMargin = (position.initialMarginInUsd - position.initialMarginInUsdFromBalance)
                .mul(position.initialMargin)
                .div(position.initialMarginInUsd);
            changeAmount = amount.toUint256().min(borrowMargin);
            position.initialMarginInUsdFromBalance += changeAmount.mul(position.initialMarginInUsd).div(
                position.initialMargin
            );
        } else {
            uint256 addBorrowMarginInUsd = (-amount).toUint256().mul(position.initialMarginInUsd).div(
                position.initialMargin
            );
            if (position.initialMarginInUsdFromBalance <= addBorrowMarginInUsd) {
@==> here. 
               position.initialMarginInUsdFromBalance = 0;
                changeAmount = position.initialMarginInUsdFromBalance.mul(position.initialMargin).div(
                    position.initialMarginInUsd
                );
            } else {
                position.initialMarginInUsdFromBalance -= addBorrowMarginInUsd;
                changeAmount = (-amount).toUint256();
            }
        }
        if (needSendEvent && changeAmount > 0) {
            position.emitPositionUpdateEvent(requestId, Position.PositionUpdateFrom.DEPOSIT, 0);
        }
    }
```

## Impact
Traders' cross margin's `initialMarginInUsdFromBalance` is not correct. This will lead to some other issues based on `initialMarginInUsdFromBalance`. For example, function `getCrossAvailableValue` aims to get one account's available
value. This will be impacted by this vulnerability. Traders may not withdraw same amount collateral like they expect.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L303-L338
## Tool used

Manual Review

## Recommendation
Calculate `changeAmount` at first.