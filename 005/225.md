Great Maroon Wasp

medium

# The check for the user’s collateralUserCap is missing params.amount in AssetsProcess::deposit()


## Summary
The check for the user’s collateralUserCap is missing params.amount in AssetsProcess::deposit()
## Vulnerability Detail
```javascript
function deposit(DepositParams calldata params) external {
        address token = depositToVault(params);
        Account.Props storage accountProps = Account.loadOrCreate(params.account);
        if (DepositFrom.MANUAL == params.from) {
            AppTradeTokenConfig.TradeTokenConfig memory tradeTokenConfig = AppTradeTokenConfig.getTradeTokenConfig(
                token
            );
            if (!tradeTokenConfig.isSupportCollateral) {
                revert Errors.TokenIsNotSupportCollateral();
            }
            CommonData.Props storage commonData = CommonData.load();
            uint256 collateralAmount = commonData.getTradeTokenCollateral(token);
            if (collateralAmount + params.amount > tradeTokenConfig.collateralTotalCap) {
                revert Errors.CollateralTotalCapOverflow(token, tradeTokenConfig.collateralTotalCap);
            }
@>>>            if (accountProps.getTokenAmount(token) > tradeTokenConfig.collateralUserCap) {
                revert Errors.CollateralUserCapOverflow(token, tradeTokenConfig.collateralUserCap);
            }
            commonData.addTradeTokenCollateral(token, params.amount);
        }
        if (accountProps.owner == address(0)) {
            accountProps.owner = params.account;
        }
        accountProps.addToken(token, params.amount, Account.UpdateSource.DEPOSIT);
        if (DepositFrom.MANUAL == params.from) {
            uint256 repayAmount = accountProps.repayLiability(token);
            if (params.amount > repayAmount) {
                uint256 requestId = UuidCreator.nextId(DEPOSIT_ID_KEY);
                PositionMarginProcess.updateAllPositionFromBalanceMargin(
                    requestId,
                    params.account,
                    token,
                    (params.amount - repayAmount).toInt256(),
                    ""
                );
            }
        }

        emit Deposit(params);
    }
```
We can see that the check for the user’s collateralUserCap is missing params.amount. This makes it easy to bypass the check for the user’s collateralUserCap. For example, in the initial staking scenario, since accountProps.getTokenAmount(token) is 0, params.amount can be 10 times the tradeTokenConfig.collateralUserCap or even a larger value.

## Impact
This makes it easy to bypass the check for the user’s collateralUserCap. The user may have staked high-risk tokens exceeding the threshold. This poses a potential risk to the protocol.
## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/AssetsProcess.sol#L81

## Tool used

Manual Review

## Recommendation
Consider params.amount when checking the user’s collateralUserCap.
