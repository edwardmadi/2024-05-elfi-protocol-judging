Proper Tartan Llama

High

# User with two or more cross margin positions can update all positions from balance margin at a time.

## Summary
`PositionMarginProcess.sol#updateAllPositionFromBalanceMargin` function update all cross margin positions from balance margin by `amount` at a time.

## Vulnerability Detail
`PositionMarginProcess.sol#updateAllPositionFromBalanceMargin` function is the following.
```solidity
    function updateAllPositionFromBalanceMargin(
        uint256 requestId,
        address account,
        address token,
        int256 amount,
        bytes32 originPositionKey
    ) external {
        if (amount == 0) {
            return;
        }
        bytes32[] memory positionKeys = Account.load(account).getAllPosition();
285:    int256 reduceAmount = amount;
        for (uint256 i; i < positionKeys.length; i++) {
            Position.Props storage position = Position.load(positionKeys[i]);
            if (token == position.marginToken && position.isCrossMargin) {
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
                    amount
                ).toInt256();
295:            reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
294:            if (reduceAmount == 0) {
                    break;
                }
            }
        }
    }
```
The `reduceAmount` is initialized by `amount` in `L285` and reduced by `changeAmount` in `L295` of the loop.
But the call to `updatePositionFromBalanceMargin` uses `amount` which has always the same value.
So the function can update `amount` * number of cross margin at the extreme case.

Example:
1. Assume that `amount = 10000`.
2. Assume that user has two cross margin positions and the `changeAmount` for the first cross margin position is less than `10000` so the condition of `L294` does not hold.
3. But the total sum of `changeAmount` for all cross margin positions might be bigger than `10000`. In fact it can be `2 * 10000` at maximum.

## Impact
User with two or more cross margin positions can update all positions from balance margin at a time.
This problem causes serious damage to the protocol.

## Code Snippet
https://github.com/sherlock-audit/2024-05-elfi-protocol/blob/main/elfi-perp-contracts/contracts/process/PositionMarginProcess.sol#L293

## Tool used
Manual Review

## Recommendation
Modify the `PositionMarginProcess.sol#updateAllPositionFromBalanceMargin` function as follows.
```solidity
    function updateAllPositionFromBalanceMargin(
        uint256 requestId,
        address account,
        address token,
        int256 amount,
        bytes32 originPositionKey
    ) external {
        if (amount == 0) {
            return;
        }
        bytes32[] memory positionKeys = Account.load(account).getAllPosition();
        int256 reduceAmount = amount;
        for (uint256 i; i < positionKeys.length; i++) {
            Position.Props storage position = Position.load(positionKeys[i]);
            if (token == position.marginToken && position.isCrossMargin) {
                int256 changeAmount = updatePositionFromBalanceMargin(
                    position,
                    originPositionKey.length > 0 && originPositionKey == position.key,
                    requestId,
--                  amount
++                  reduceAmount
                ).toInt256();
                reduceAmount = amount > 0 ? reduceAmount - changeAmount : reduceAmount + changeAmount;
                if (reduceAmount == 0) {
                    break;
                }
            }
        }
    }
```